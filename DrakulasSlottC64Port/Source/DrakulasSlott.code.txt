10 POKE 53280,6 : POKE 53281,0 : PRINT "Ÿ"
20 PRINT "“TT            DRAKULAS SLOTT" : REM "TTTTTT
100 DIM D$(18),O$(28),L(22),P(18, 6) : L = 1 : L2 = L
110 FOR X = 1 TO 18 : READ D$(X) : NEXT: DATA HALLEN, LÄSRUMMET, BIBLOTEKET, VAPEN KAMMAREN
120 DATA TORNET, LÄGRE TORNET, KAPELLET, ELDSTADEN AV TEGEL, DEN GÖMDA KORRIDOREN
130 DATA DEN HEMLIGA PASSAGEN, EN UNDERJORDISK SJÖ, EN BÅT, ALKEMISTENS LABRATORIUM
140 DATA FÖRVARINGS RUMMET, TAKSKÄGGET, GALLERIET, SIDO RUMMET, WAMPYRERNAS GRAV
150 FOR X = 1 TO 28 : READ O$(X)
151 O$ = O$ +LEFT$(O$(X),2)
152 IF X>6 THEN READ L(X-6)
160 NEXT
162 DATA NORR, SÖDER, ÖST, VÄST, UPP, NER, SLÄGGA,5
170 DATA KLOCKA,1, REP,9
171 DATA PERGAMENT RULLE,3, YXA,4, ÅROR,6
172 DATA NYCKEL,7, HELIGT VATTEN,7
180 DATA OLJE FLASKA,13, LÅDA,14
181 DATA HINK,14
184 DATA FACKLA,8
186 DATA SPIKAR
187 DATA 15
193 DATA GOBELÄNG,16, BÅT,11, ROSTIG DÖRR,17, STÄNGD KISTA,18
195 DATA ELD I ELDSTADEN,2, BOKHYLLA,3
200 DATA SKYLT,1, BALKONG,5
201 DATA ELDSTAD AV TEGEL,2
203 FOR Y = 1 TO 18 : FOR X = 1 TO 6 : READ P(Y, X) : NEXT X,Y
210 DATA ,,3,2,,,,,1,,,,,,4,1,,,,,5,3,,,,,,4,,,,7,,,5,,6,,,,4,
220 DATA ,2,,,,,13,,,,3,,11,8,,,,,,10,,,,,,11,,,,,14,9,,,,,,13,,,2,,
230 DATA ,,,,16,,,,,,,,16,,,,,,17,,,,,
240 W$ = "GÅTASELÄSLÖPBIFLDÖOLRO"
245 T2 = 20
250 PRINT "QJAG ÄR I "; D$(L) : Y = 0 : L2 = L : PRINT
260 IF L(12)<> 0  AND (L = 10 OR L = 11 OR L = 12 OR L = 18) THEN 280
270 GOTO 290
280 PRINT "DET ÄR MÖRKT JAG KAN INTE SE" : GOTO 330
290 PRINT "JAG SER "; : FOR X = 1 TO 22 : IF(L(X) = L2) THEN PRINT " *"O$(X + 6); : Y = Y + 1
300 NEXT: IF Y = 0 THEN PRINT "INGENTING‘"
305 PRINT : PRINT
310 PRINT "SYNLIGA UTGÅNGAR - "; : FOR X = 1 TO 6 : IF P(L, X)> 0 THEN PRINT O$(X); "* ";
320 NEXT: PRINT
330 PRINT: S = 0 : F = 0 : INPUT "VAD SKALL JAG GÖRA "; A$ : PRINT: B$ = ""
335 FOR X = 1 TO LEN(A$)
340 IF MID$(A$, X,1) = " " AND LEN(A$)> X + 1 THEN B$ = MID$(A$, X + 1,2)
350 NEXT: GOSUB 360: GOTO 380
360 IF LEN(A$)> 1 THEN A$ = LEFT$(A$,2)
370 RETURN
380 L2 = L
385 T1 = T1 + 1 : IF T1 = 60 THEN T1 = 0 : T2 = T2 + 1
400 IF B$ = "DÖ" THEN B$ = "RO"
405 IF T2 = 24 THEN PRINT "VAMPYREN HAR RYMT - DIN TID ÄR UTE !" : GOTO 1200
410 IF B$ = "FL" THEN B$ = "OL"
415 IF A$ = "DÖ" AND B$ = "VA" AND L = 18 THEN 1060
425 IF B$ = "KI" THEN B$ = "ST"
430 IF A$ = "SI" AND L = 11 THEN PRINT "DU DRUNKNADE I DET ISKALLA VATTNET" : GOTO 1200
432 IF A$ = "SI" THEN PRINT "JAG SER INGET VATTEN" : GOTO 330
435 IF A$ = "GÅ" AND B$ = "TA" AND L = 16 THEN L = 15 : GOTO 250
437 IF B$ = "VA" THEN B$ = "HE"
440 IF A$ = "HJ" THEN 1150
445 IF (B$ = "TR" OR B$ = "PÅ") AND O$(16) = "TRÄPÅLAR" THEN B$ = "LÅ"
450 FOR X = 1 TO LEN(W$) STEP 2: IF MID$(W$,X,2) = A$ THEN F = (X + 1) / 2
460 NEXT : FOR X = 1 TO LEN(O$) STEP 2: IF MID$(O$,X,2) = B$ THEN S = (X + 1) / 2
470 NEXT : IF A$<>"IN" THEN 500
480 PRINT "JAG BÄR FÖLJANDE SAKER" : A = 0 : FOR X = 1 TO 22 : IF L(X) = 0 THEN PRINT O$(X + 6) : A = A + 1
490 NEXT: IF A> 0 THEN 330
495 PRINT "INGENTING" : GOTO 330
500 IF F<1 THEN PRINT "JAG FÖRSTÅR INTE !" : GOTO 330
510 ON F - 1 GOTO 640,790,870,905,950,1000,1030,1050,1100,1120
512 IF A$<>"GÅ" THEN F = -1 : GOTO 500
520 IF S<1 OR S>6 THEN 540
530 IF P(L, S)>0 THEN L = P(L, S) : GOTO 250
540 IF B$<>"EL" OR L<>2 THEN 570
550 IF FI = 0 THEN PRINT "DU BRÄNDES TILL DÖDS" : GOTO 1200
560 L = 8 : GOTO 250
570 IF L<>5 OR B$<>"BA" THEN 600
580 IF TGP$ = "BA" THEN L = 6 : GOTO 250
590 PRINT "DU FÖLL OCH DOG" : GOTO 1200
600 IF(L(15) = L)  AND (B$ = "BÅ") THEN L = 12 : GOTO 250
610 IF L<>16 OR B$<>"TA" THEN 670
620 IF (L(10) = L) AND(O$(16) = "LÅDA") THEN L = 15 : GOTO 250
630 PRINT "KAN INTE ÄNNU, DET ÄR FÖR HÖGT" : GOTO 330
640 IF S<7 THEN F = 0 : GOTO 500
645 IF C>6 THEN PRINT "JAG BÄR FÖR MYCKET" : GOTO 330
650 IF S<>14 THEN 690
660 INPUT "I VAD "; A$ : GOSUB 360
665 IF(A$ = "HI")   AND  (L(11) = 0)  AND (L(8) = L) THEN 680
670 PRINT "KAN INTE" : GOTO 330
680 L(8) = 0 : C = C + 1 : GOTO 250
690 IF S<>20 THEN 720
695 IF (L(14)<> L) THEN 765
700 IF TA = 1 THEN L(14) = 0 : PRINT "AHA!" : P(16, 1) = 17 : GOTO 330
710 PRINT "DEN ÄR FASTSPIKAD VID TAKSKÄGGET" : GOTO 330
720 IF S<>19  THEN  730
723 IF L(13) = L   AND  L(1) = 0 THEN TA = 1 : PRINT "GOBELÄNGEN FÖLL" : GOTO 780
725 IF L(1)<> 0  THEN PRINT "INGEN SLÄGGA" : GOTO 330
730 IF S = 19  AND  (L(13)<> L) THEN 765
740 IF B$ = "" OR S<7 THEN 250
750 IF  S>20  THEN  670
760 IF  (L(S-6) = L) THEN 780
765 PRINT "JAG SER DEN INTE" : GOTO 330
780 C = C + 1 : L(S - 6) = 0 : GOTO 250
790 IF B$ = "" OR S<7 THEN 250
800 IF  S<>26  THEN  840
810 IF  L<>1  THEN  S = 23 : GOTO 760
820 PRINT "VAMPYREN VAKNAR VID MIDNATT"
830 GOTO 330
840 IF S<10 THEN 865
850 IF  L(4) = 0 THEN 860
855 PRINT "DEN HAR JAG INTE" : GOTO 330
860 PRINT "ALLA UTGÅNGAR ÄR INTE SYNLIGA" : GOTO 330
865 IF S<>8 THEN 330
867 IF L(2)<> 0 THEN 855
869 PRINT T2; " : "; T1: GOTO 330
870 IF S = 14 AND L(8) = 0 AND L = 2 THEN O$(24) = "ASKA" : FI = 1 : L(8) = 99 : C = C - 1 : GOTO 250
880 IF B$ = "" OR S<7 THEN PRINT "VA ??" : GOTO 330
890 IF L(S-6)<> 0 THEN 855
900 L(S - 6) = L : C = C - 1 : GOTO 250
905 IF(S <> 16 OR L(10) <> L OR L(5) <> 0)  AND (S <> 28 OR L(1) <> 0 OR L <> 8) THEN 930
910 INPUT "MED VAD "; A$ : GOSUB 360
920 IF A$ = "YX" AND S = 16 THEN 940
925 IF (A$ = "SL") AND S = 28 THEN 935
930 PRINT "QINGENTING HÄNDE" : GOTO 330
935 O$(28) = "EN SÖNDERSLAGEN ELDSTAD" : P(8, 1) = 10 : D$(8) = O$(28) : GOTO 250
940 O$(16) = "TRÄPÅLAR" : PRINT "QDU SLOG SÖNDER LÅDAN TILL NÅGRA TRÄPÅLAR"; : GOTO 250
950 IF S = 23  AND  L = 18 AND L(7) = 0 THEN 980
960 IF L = 17 AND S = 22 AND OI = 1 AND L(7) = 0 THEN 990
970 GOTO 930
980 O$(23) = "VAMPYR I KISTA" : GOTO 250
990 O$(22) = "ÖPPEN DÖRR" : P(17, 1) = 18 : GOTO 250
1000 IF L(3)<> 0 OR S<>9 THEN 670
1010 INPUT"I VAD"; A$ : GOSUB 360
1020 TGP$ = A$ : GOTO 250
1030 IF B$ = "OL" OR B$ = "FL" THEN PRINT "MENAR DU ATT JAG BÖR OLJA DEN ?" : GOTO 250
1035 IF S<>25 OR L<>3 THEN 670
1040 PRINT "AHA!" : P(L, 6) = 9 : GOTO 250
1050 IF S<>23 OR L<>18 OR O$(23)<> "VAMPYR I KISTAN" THEN 670
1060 INPUT "MED VAD "; A$ : GOSUB 360
1070 IF A$ = "TR" OR  A$ = "PÅ" AND L(10) = 0 AND  O$(16) = "TRÄPÅLAR" THEN 1090
1080 PRINT "QDU GJORDE FEL ! VAMPYREN VAKNADE OCH DÖDADE DIG" : GOTO 1200
1090 PRINT "QGRATULERAR ! - DU HAR DÖDAT VAMPYREN" : GOTO 1200
1100 IF L(9)<> 0 OR L<>17 OR (S<>22 AND S<>6) THEN 670
1110 OI = 1 : O$(22) = "ÖPPEN DÖRR" : P(17, 1) = 18 : GOTO 250
1120 IF L<>12 OR L(6)<> 0 THEN 670
1130 PRINT "DU HAR ROTT MED BÅTEN TILL ANDRA SIDAN" : L = 16
1132 FOR X = 1 TO 22
1133 IF  L(X) = 12 THEN L(X) = 16
1135 NEXT
1140 L(15) = L : GOTO 250
1150 PRINT "“QQVÄDERSTRÄCKEN ÄR:"
1160 PRINT "QNORR,SÖDER,ÖST,VÄST,NER,UPP"
1165 PRINT "QQVERBEN ÄR:"
1168 PRINT "QGÅ,TA,SE,LÄGGA,SLÄPPA,ÖPPNA,BINDA,FLYTTA"
1170 PRINT "QDÖDA,OLJA,RO,SIMMA"
1180 PRINT "Q" : GOTO 330
1200 PRINT "QQRF1’ SPELA DRAKULAS SLOTT EN GÅNG TILL"
1210 PRINT "QRF3’ LADDA NÄSTA PROGRAM"
1220 PRINT "QTRYCK RF1’ ELLER RF3’ !"
1230 GETA$ : IF A$ = "" THEN 1230
1240 IF A$ = "…" THEN RUN
1250 IF A$ = "†" THEN PRINT "“QQTRYCK PLAY PA‘.Q BANDSPELAREN !" : LOAD
1260 GOTO 1230